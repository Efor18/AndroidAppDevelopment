// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // TODO necessary but why?
        classpath 'com.android.tools.build:gradle:1.0.0-rc4'
        // include coveralls plugin for coverage reports
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:1.0.2'
    }
}

// some defaults for all sub projects
configure(allprojects) { project ->
    buildscript {
        repositories {
            jcenter()
        }
    }
}

// some defaults for all sub projects
subprojects {
    repositories {
        // common dependency repository
        jcenter()
        maven { url "file://${System.getenv("ANDROID_HOME")}/extras/android/m2repository" }
        mavenLocal()
    }
}

// Improve build server performance by allowing disabling of pre-dexing
// (see http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance.)
project.ext.preDexLibs = !project.hasProperty('disablePreDex')
ext.robolectricDependsOnNeeded = true;
subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

// ensure clean is also triggered for root build folder
apply plugin: 'java'
apply plugin: 'build-dashboard'

buildDashboard {
    reports.html.destination = "build/"
}

// clean up dashboard from not generated reports
test.reports.html.enabled = false
test.reports.junitXml.enabled = false

// Android Studio Support - avoid missing task error at compiling test classes before test execution
task testCompile {}

// code coverage for all unit tests
apply from: 'Scripts/jacoco-coveralls-support.gradle'